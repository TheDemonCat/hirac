#Использовать irac

Перем ПараметрыПодключения;
Перем Агенты;
Перем ОписанияКластеров;
Перем ОписанияСерверов;
Перем ОписанияРабочихПроцессов;
Перем ОписанияИнформационныхБаз;
Перем ОписанияСеансов;
Перем ОписанияСоединений;

#Область ОбработчикиСобытийОбъекта

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// Параметры:
//   НастройкиПодключения     - Строка,     - путь к файлу настроек управления кластерами
//                              Структура     или структура настроек управления кластерами
//
Процедура ПриСозданииОбъекта(Знач НастройкиПодключения = Неопределено)

	ИнициализироватьАгентыУправленияКластерами(НастройкиПодключения);

КонецПроцедуры // ПриСозданииОбъекта()

#КонецОбласти // ОбработчикиСобытийОбъекта

#Область ПрограммныйИнтерфейс

// Функция - возвращает структуру с подключениями к агентам управления кластерами
//
// Возвращаемое значение:
//   Структура     - структура с подключениями к агентам управления кластерами
//
Функция Агенты() Экспорт
	
	Возврат Агенты;

КонецФункции // Агенты()

// Функция - возвращает структуру параметров подключения к агентам управления кластерами
//
// Возвращаемое значение:
//   Структура     - структура параметров подключения к агентам управления кластерами
//
Функция ПараметрыПодключения() Экспорт
	
	Возврат ПараметрыПодключения;

КонецФункции // ПараметрыПодключения()

// Процедура инициализирует подключения к агентам управления кластерами
//
// Параметры:
//   НастройкиПодключения     - Строка,     - путь к файлу настроек управления кластерами
//                              Структура     или структура настроек управления кластерами
//
Процедура ИнициализироватьАгентыУправленияКластерами(Знач НастройкиПодключения = Неопределено) Экспорт
	
	Если ТипЗнч(НастройкиПодключения) = Тип("Структура") Тогда
		ПараметрыПодключения = НастройкиПодключения;
	ИначеЕсли ТипЗнч(НастройкиПодключения) = Тип("Строка") Тогда
		ПараметрыПодключения = ОбщегоНазначения.ПрочитатьДанныеИзМакетаJSON(НастройкиПодключения, Истина);
	Иначе
		ПараметрыПодключения = ОбщегоНазначения.ПрочитатьДанныеИзМакетаJSON("/config/racsettings", Истина);
	КонецЕсли;

	Агенты = Новый Соответствие();

	ПараметрыСервисов  = ОбщегоНазначения.ПолучитьЗначениеНастройки("ras"    , ПараметрыПодключения);
	ПараметрыКластеров = ОбщегоНазначения.ПолучитьЗначениеНастройки("cluster", ПараметрыПодключения);

	Для Каждого ТекПараметры Из ПараметрыСервисов Цикл
	
		Если ВРег(ТекПараметры.Ключ) = "__DEFAULT" И НЕ ПараметрыСервисов.Количество() = 1 Тогда
			Продолжить;
		КонецЕсли;

		УправлениеКластером = ИнициализироватьАгента(ПараметрыПодключения, ТекПараметры.Ключ);

		ОписаниеАгента = Новый Структура();
		ОписаниеАгента.Вставить("Имя"        , ВРег(ТекПараметры.Ключ));
		ОписаниеАгента.Вставить("Резервирует", ВРег(ТекПараметры.Значение["reserves"]));
		ОписаниеАгента.Вставить("Агент"      , УправлениеКластером);

		Агенты.Вставить(ВРег(ТекПараметры.Ключ), ОписаниеАгента);

		УстановитьПараметрыКластеровАгента(УправлениеКластером, ПараметрыКластеров);

	КонецЦикла;

КонецПроцедуры // ИнициализироватьАгентыУправленияКластерами()

Функция ОписанияОбъектовКластера(Знач ТипОбъекта, Знач Обновить = Ложь,
	                             Знач Поля = "_all", Знач Фильтр = Неопределено) Экспорт

	ОбновитьКэшОписанийОбъектовКластеров(ТипОбъекта, Обновить);

	КэшОписанийОбъектов = КэшОписанийОбъектовКластера(ТипОбъекта);

	Результат = Новый Массив();

	ОбновленныеОписания = Новый Соответствие();

	Для Каждого ТекОписаниеОбъекта Из КэшОписанийОбъектов Цикл

		Если НЕ ОбщегоНазначения.ОбъектСоответствуетФильтру(ТекОписаниеОбъекта.Значение, Фильтр) Тогда
			Продолжить;
		КонецЕсли;

		ОписаниеОбъекта = ОписаниеОбъектаКластера(ТипОбъекта, ТекОписаниеОбъекта.Значение, Поля, ОбновленныеОписания);

		Результат.Добавить(ОписаниеОбъекта);

	КонецЦикла;

	Для Каждого ТекОписаниеОбъекта Из ОбновленныеОписания Цикл
		КэшОписанийОбъектов.Вставить(ТекОписаниеОбъекта.Ключ, ТекОписаниеОбъекта.Значение);
	КонецЦикла;

	Возврат Результат;

КонецФункции // ОписанияОбъектовКластера()

#КонецОбласти // ПрограммныйИнтерфейс

#Область ПолучениеДанныхСервисаАдминистрирования

Функция ПолныеОписанияКластеровАгента(Знач Агент)

	ОписанияОбъектовАгента = Новый Соответствие();

	СписокКластеров = Агент.Кластеры().Список(, , Истина);

	ПоляОбъекта = ТипыОбъектовКластера.СвойстваОбъекта(ТипыОбъектовКластера().Кластеры);

	Для Каждого ТекКластер Из СписокКластеров Цикл

		ПолноеОписаниеОбъекта = Новый Соответствие();

		ДополнитьОписаниеОбъектаКластера(ПолноеОписаниеОбъекта, ТипыОбъектовКластера().Кластеры, Агент, ТекКластер);

		Для Каждого ТекПолеОбъекта Из ПоляОбъекта Цикл
			
			ИмяПоля = ТекПолеОбъекта.ИмяРАК;

			ЗначениеЭлемента = ТекКластер[ТекПолеОбъекта.Имя];

			ПолноеОписаниеОбъекта.Вставить(ИмяПоля, ЗначениеЭлемента);

			РасширитьПолеОбъектаКластера(ПолноеОписаниеОбъекта, ИмяПоля, ТипыОбъектовКластера().Кластеры, ТекКластер, Агент);

		КонецЦикла;

		ОписанияОбъектовАгента.Вставить(ПолноеОписаниеОбъекта["cluster"], ПолноеОписаниеОбъекта);

	КонецЦикла;

	Возврат ОписанияОбъектовАгента;

КонецФункции // ПолныеОписанияКластеровАгента()

Функция ПолныеОписанияОбъектовАгента(Знач Агент, Знач ТипОбъекта)

	Если ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Кластеры) Тогда
		Возврат ПолныеОписанияКластеровАгента(Агент);
	КонецЕсли;

	ПолныеОписанияОбъектов = Новый Соответствие();

	СписокКластеров = Агент.Кластеры().Список();

	Для Каждого ТекКластер Из СписокКластеров Цикл

		ПолныеОписанияОбъектовКластера = ПолныеОписанияОбъектовКластера(ТекКластер, ТипОбъекта, Агент);

		Для Каждого ТекОписаниеОбъекта Из ПолныеОписанияОбъектовКластера Цикл
		
			ПолныеОписанияОбъектов.Вставить(ТекОписаниеОбъекта[ТипОбъекта], ТекОписаниеОбъекта);

		КонецЦикла;

	КонецЦикла;

	Возврат ПолныеОписанияОбъектов;

КонецФункции // ПолныеОписанияОбъектовАгента()

Функция ПолныеОписанияОбъектовКластера(Знач Кластер, Знач ТипОбъекта, Знач Агент)

	ПолныеОписанияОбъектов = Новый Массив();
	
	СписокОбъектовКластера = Кластер.Получить(ТипОбъекта).Список(, , Истина);

	ПоляОбъекта = ТипыОбъектовКластера.СвойстваОбъекта(ТипОбъекта);

	Для Каждого ТекОбъектКластера Из СписокОбъектовКластера Цикл

		ПолноеОписаниеОбъекта = Новый Соответствие();

		ДополнитьОписаниеОбъектаКластера(ПолноеОписаниеОбъекта, ТипОбъекта, Агент, Кластер);

		Для Каждого ТекПолеОбъекта Из ПоляОбъекта Цикл

			ИмяПоля = ТекПолеОбъекта.ИмяРАК;

			ЗначениеЭлемента = ТекОбъектКластера[ТекПолеОбъекта.Имя];
			
			ПолноеОписаниеОбъекта.Вставить(ИмяПоля, ЗначениеЭлемента);

			РасширитьПолеОбъектаКластера(ПолноеОписаниеОбъекта,
			                             ИмяПоля,
			                             ТипОбъекта,
			                             ТекОбъектКластера,
			                             Кластер,
			                             Агент);

		КонецЦикла;

		ПолныеОписанияОбъектов.Добавить(ПолноеОписаниеОбъекта);

	КонецЦикла;

	Возврат ПолныеОписанияОбъектов;
	
КонецФункции // ПолныеОписанияОбъектовКластера()

Процедура РасширитьПолеОбъектаКластера(ПолноеОписаниеОбъекта, ИмяПоля, ТипОбъекта, ОбъектКластера, Кластер = Неопределено, Агент = Неопределено)

	Если ТипОбъекта = ТипыОбъектовКластера().Кластеры
	 ИЛИ ТипОбъекта = ТипыОбъектовКластера().Серверы
	 ИЛИ ТипОбъекта = ТипыОбъектовКластера().РабочиеПроцессы Тогда
		АдресСервера = ОбъектКластера.Получить("АдресСервера");
		МеткаОбъектаКластера = СтрШаблон("%1:%2", АдресСервера, ОбъектКластера.Получить("ПортСервера"));
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().Кластеры
	   И ВРег(ИмяПоля) = "CLUSTER" Тогда
	   ПолноеОписаниеОбъекта.Вставить("cluster-label", МеткаОбъектаКластера);
	КонецЕсли;
	Если (ТипОбъекта = ТипыОбъектовКластера().Кластеры ИЛИ ТипОбъекта = ТипыОбъектовКластера().Серверы)
	   И ВРег(ИмяПоля) = "NAME" Тогда
		ОбщегоНазначения.УбратьКавычки(ПолноеОписаниеОбъекта[ИмяПоля]);
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().Серверы
	   И ВРег(ИмяПоля) = "SERVER" Тогда
		АдресСервера = ОбъектКластера["АдресАгента"];
		МеткаСервера = СтрШаблон("%1:%2", АдресСервера, ОбъектКластера["ПортАгента"]);
		ПолноеОписаниеОбъекта.Вставить("server-host" , АдресСервера);
		ПолноеОписаниеОбъекта.Вставить("server-label", МеткаСервера);
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().РабочиеПроцессы
	   И ВРег(ИмяПоля) = "PROCESS" Тогда
		ПолноеОписаниеОбъекта.Вставить("process-host" , АдресСервера);
		ПолноеОписаниеОбъекта.Вставить("process-label", МеткаОбъектаКластера);
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().ИнформационныеБазы
	   И ВРег(ИмяПоля) = "DESCR" Тогда
	   ОбщегоНазначения.УбратьКавычки(ПолноеОписаниеОбъекта[ИмяПоля]);
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().Сеансы 
	 ИЛИ ТипОбъекта = ТипыОбъектовКластера().Соединения Тогда
		Если ВРег(ИмяПоля) = "INFOBASE" Тогда
			ПолноеОписаниеОбъекта.Вставить("infobase-label", "_none");
			ОписаниеИБ = Неопределено;
			Если ТипЗнч(ОписанияИнформационныхБаз) = Тип("Соответствие") Тогда
				ОписаниеИБ = ОписанияИнформационныхБаз[ПолноеОписаниеОбъекта[ИмяПоля]];
			КонецЕсли;
			Если ОписаниеИБ = Неопределено И НЕ ПолноеОписаниеОбъекта[ИмяПоля] = ОбщегоНазначения.ПустойГУИД() Тогда
				ОписанияИнформационныхБаз = ПолныеОписанияОбъектовАгента(Агент, ТипыОбъектовКластера().ИнформационныеБазы);
				ОписаниеИБ = ОписанияИнформационныхБаз[ПолноеОписаниеОбъекта[ИмяПоля]];
			КонецЕсли;
			Если НЕ ОписаниеИБ = Неопределено Тогда
				ПолноеОписаниеОбъекта.Вставить("infobase-label", ОписаниеИБ["name"]);
			КонецЕсли;
		ИначеЕсли ВРег(ИмяПоля) = "PROCESS" Тогда
			ПолноеОписаниеОбъекта.Вставить("process-label", "_none");
			ПолноеОписаниеОбъекта.Вставить("process-host" , "_none");
			ОписаниеПроцесса = Неопределено;
			Если ТипЗнч(ОписанияРабочихПроцессов) = Тип("Соответствие") Тогда
				ОписаниеПроцесса = ОписанияРабочихПроцессов[ПолноеОписаниеОбъекта[ИмяПоля]];
			КонецЕсли;
			Если ОписаниеПроцесса = Неопределено И НЕ ПолноеОписаниеОбъекта[ИмяПоля] = ОбщегоНазначения.ПустойГУИД() Тогда
				ОписанияРабочихПроцессов = ПолныеОписанияОбъектовАгента(Агент, ТипыОбъектовКластера().РабочиеПроцессы);
				ОписаниеПроцесса = ОписанияРабочихПроцессов[ПолноеОписаниеОбъекта[ИмяПоля]];
			КонецЕсли;
			Если НЕ ОписаниеПроцесса = Неопределено Тогда
				ПолноеОписаниеОбъекта.Вставить("process-label",
				                               СтрШаблон("%1:%2", ОписаниеПроцесса["host"], ОписаниеПроцесса["port"]));
				ПолноеОписаниеОбъекта.Вставить("process-host" , ОписаниеПроцесса["host"]);
			КонецЕсли;
		ИначеЕсли ТипОбъекта = ТипыОбъектовКластера().Соединения
				И ВРег(ИмяПоля) = "APPLICATION" Тогда
			ОбщегоНазначения.УбратьКавычки(ПолноеОписаниеОбъекта[ИмяПоля]);
		КонецЕсли;
	КонецЕсли;
		
	Если ВРег(ИмяПоля) = "STARTED-AT"
	 ИЛИ ВРег(ИмяПоля) = "CONNECTED-AT" Тогда
		Попытка
			ПолноеОписаниеОбъекта.Вставить("duration", ТекущаяДата() - ПолноеОписаниеОбъекта[ИмяПоля]);
		Исключение
			ПолноеОписаниеОбъекта.Вставить("duration", 0);
		КонецПопытки;
	КонецЕсли;
 
КонецПроцедуры // РасширитьПолеОбъектаКластера()

Процедура ДополнитьОписаниеОбъектаКластера(ПолноеОписаниеОбъекта, ТипОбъекта, Агент, Кластер)


	МеткаАгента = СтрШаблон("%1:%2", Агент.АдресСервераАдминистрирования(), Агент.ПортСервераАдминистрирования());

	ПолноеОписаниеОбъекта.Вставить("agent", МеткаАгента);
	ПолноеОписаниеОбъекта.Вставить("_clusterObject", Кластер);

	Если НЕ ТипОбъекта = ТипыОбъектовКластера().Кластеры Тогда
		ПолноеОписаниеОбъекта.Вставить("cluster"      , Кластер.Ид());
		ПолноеОписаниеОбъекта.Вставить("cluster-host" , Кластер.АдресСервера());
		ПолноеОписаниеОбъекта.Вставить("cluster-port" , Кластер.ПортСервера());
		ПолноеОписаниеОбъекта.Вставить("cluster-label",
		                                 СтрШаблон("%1:%2", Кластер.АдресСервера(), Кластер.ПортСервера()));
	КонецЕсли;
	
	ПолноеОписаниеОбъекта.Вставить("count", 1);

КонецПроцедуры // ДополнитьОписаниеОбъектаКластера()

Функция ПолеОсновнойИнформацииИБ(ИмяПоля)

	КраткиеСведения = "INFOBASE, NAME, CLUSTER, AGENT, DESCR, COUNT, _NO, _SUMMARY";

	Возврат НЕ Найти(КраткиеСведения, ВРег(ИмяПоля)) = 0;

КонецФункции // ПолеОсновнойИнформацииИБ()

Функция НужноДобавлятьПоле(ТипОбъекта, ДобавляемыеПоля, ИмяПоля)

	Если ВРег(ИмяПоля) = "_CLUSTEROBJECT" Тогда
		Возврат Ложь;
	КонецЕсли;

	Если НЕ ДобавляемыеПоля.Найти("_ALL") = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	Если ТипОбъекта = ТипыОбъектовКластера().ИнформационныеБазы Тогда
		Если НЕ ДобавляемыеПоля.Найти("_SUMMARY") = Неопределено И ПолеОсновнойИнформацииИБ(ИмяПоля) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Если НЕ ДобавляемыеПоля.Найти(ВРег(ИмяПоля)) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;

КонецФункции // НужноДобавлятьПоле()

Процедура ОбновитьКэшОписанийОбъектовКластеров(Знач ТипОбъекта, Знач Обновить = Ложь)

	Если НЕ НужноОбновитьОписанияОбъектовКластера(ТипОбъекта, Обновить) Тогда
		Возврат;
	КонецЕсли;

	ОписанияОбъектовКластеров = КэшОписанийОбъектовКластера(ТипОбъекта, Истина);
	Если ТипЗнч(ОписанияОбъектовКластеров) = Тип("Соответствие") Тогда
		ОписанияОбъектовКластеров.Очистить();
	КонецЕсли;

	Для Каждого ТекАгент Из Агенты Цикл

		Если НЕ ИспользоватьАгент(ТекАгент.Значение) Тогда
			Продолжить;
		КонецЕсли;

		ОписанияОбъектовАгента = ПолныеОписанияОбъектовАгента(ТекАгент.Значение.Агент, ТипОбъекта);

		Для Каждого ТекОписаниеОбъекта Из ОписанияОбъектовАгента Цикл
			Если ОписанияОбъектовКластеров[ТекОписаниеОбъекта.Значение[ТипОбъекта]] = Неопределено Тогда
				ОписанияОбъектовКластеров.Вставить(ТекОписаниеОбъекта.Значение[ТипОбъекта], ТекОписаниеОбъекта.Значение);
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры // ОбновитьКэшОписанийОбъектовКластеров()

Функция ОписаниеОбъектаКластера(ТипОбъекта, ПолноеОписаниеОбъекта, Знач Поля = "_all", ОбновленныеОписания = Неопределено)

	Поля = ОбщегоНазначения.СписокПолей(Поля);

	Если НЕ ТипЗнч(ОбновленныеОписания) = Тип("Соответствие") Тогда
		ОбновленныеОписания = Новый Соответствие();
	КонецЕсли;

	ОписаниеОбъекта = Новый Соответствие();

	// Для ИБ проверяем необходимость полного описания
	ПолучитьПолноеОписаниеИБ = Ложь;
	Если ТипОбъекта = ТипыОбъектовКластера().ИнформационныеБазы Тогда
		Для Каждого ТекПоле Из Поля Цикл
			Если НЕ ПолеОсновнойИнформацииИБ(ТекПоле) Тогда
				ПолучитьПолноеОписаниеИБ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Для Каждого ТекПолеОписанияОбъекта Из ПолноеОписаниеОбъекта Цикл

		ИмяПоля = ТекПолеОписанияОбъекта.Ключ;
		ЗначениеПоля = ТекПолеОписанияОбъекта.Значение;

		Если НЕ НужноДобавлятьПоле(ТипОбъекта, Поля, ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;

		// При необходимости получаем полное описание ИБ и сохраняем в кэш
		Если ПолучитьПолноеОписаниеИБ И ПолноеОписаниеОбъекта[ИмяПоля] = Неопределено Тогда
			КластерИБ = ПолноеОписаниеОбъекта["_clusterObject"];
			ИдИБ = ПолноеОписаниеОбъекта["infobase"];

			ИБКластера = КластерИБ.ИнформационныеБазы().Получить(ИдИБ);

			Если НЕ ИБКластера.ПолноеОписание() Тогда
				ИБКластера.ОбновитьДанные(Истина);
			КонецЕсли;

			Если НЕ ИБКластера.ПолноеОписание() Тогда
				ПолучитьПолноеОписаниеИБ = Ложь;
				Продолжить;
			КонецЕсли;

			ПолноеОписаниеИБКластера = КластерИБ.ИнформационныеБазы().Получить(ИдИБ, , Истина);
			ПолноеОписаниеИБКластера.Вставить("_clusterObject", КластерИБ);

			ЗначениеПоля = ИБКластера.Получить(ИмяПоля);

			ОбновленныеОписания.Вставить(ИдИБ, ПолноеОписаниеИБКластера);
		КонецЕсли;

		ОписаниеОбъекта.Вставить(ИмяПоля, ЗначениеПоля);

	КонецЦикла;

	Возврат ОписаниеОбъекта;

КонецФункции // ОписаниеОбъектаКластера()

Функция КэшОписанийОбъектовКластера(Знач ТипОбъекта, Знач Очистить = Ложь)

	Если ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Кластеры) Тогда
		Если НЕ ТипЗнч(ОписанияКластеров) = Тип("Соответствие") Тогда
			ОписанияКластеров = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияКластеров;
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Серверы) Тогда
		Если НЕ ТипЗнч(ОписанияСерверов) = Тип("Соответствие") Тогда
			ОписанияСерверов = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияСерверов;
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().РабочиеПроцессы) Тогда
		Если НЕ ТипЗнч(ОписанияРабочихПроцессов) = Тип("Соответствие") Тогда
			ОписанияРабочихПроцессов = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияРабочихПроцессов;
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().ИнформационныеБазы) Тогда
		Если НЕ ТипЗнч(ОписанияИнформационныхБаз) = Тип("Соответствие") Тогда
			ОписанияИнформационныхБаз = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияИнформационныхБаз;
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Сеансы) Тогда
		Если НЕ ТипЗнч(ОписанияСеансов) = Тип("Соответствие") Тогда
			ОписанияСеансов = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияСеансов;
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Соединения) Тогда
		Если НЕ ТипЗнч(ОписанияСоединений) = Тип("Соответствие") Тогда
			ОписанияСоединений = Новый Соответствие();
		КонецЕсли;
		КэшОписанийОбъектов = ОписанияСоединений;
	Иначе
		КэшОписанийОбъектов = Новый Соответствие();
	КонецЕсли;

	Если Очистить Тогда
		КэшОписанийОбъектов.Очистить();
	КонецЕсли;

	Возврат КэшОписанийОбъектов;

КонецФункции // КэшОписанийОбъектовКластера()

#КонецОбласти // ПолучениеДанныхСервисаАдминистрирования

#Область СлужебныеПроцедурыИФункции

// Функция - создает, устанавливает настройки и возвращает объект управления кластером 1С
//
// Параметры:
//   ПараметрыПодключения    - Соответствие   - настройки подключения к агентам администрирования
//   Сервис                  - Строка         - адрес подключения к сервису администрирования (RAS) в виде <адрес>:<порт>
//
// Возвращаемое значение:
//   УправлениеКластером1С     - объект управления кластером 1С
//
Функция ИнициализироватьАгента(ПараметрыПодключения, Сервис)
	
	ПараметрыСервисов    = ОбщегоНазначения.ПолучитьЗначениеНастройки("ras"      , ПараметрыПодключения);
	ПараметрыПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("__default", ПараметрыСервисов);
	ПараметрыПоИмени     = ОбщегоНазначения.ПолучитьЗначениеНастройки(Сервис     , ПараметрыСервисов);

	АдресСервисаПоУмолчанию  = ОбщегоНазначения.ПолучитьЗначениеНастройки("ras"       , ПараметрыПоУмолчанию);
	ВерсияКлиентаПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("rac"       , ПараметрыПоУмолчанию);
	АдминистраторПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоУмолчанию);
	ПарольПоУмолчанию        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоУмолчанию);
	
	АдресСервисаПоИмени  = ОбщегоНазначения.ПолучитьЗначениеНастройки("ras"       , ПараметрыПоИмени);
	ВерсияКлиентаПоИмени = ОбщегоНазначения.ПолучитьЗначениеНастройки("rac"       , ПараметрыПоИмени);
	АдминистраторПоИмени = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоИмени);
	ПарольПоИмени        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоИмени);

	Если НЕ АдресСервисаПоИмени = Неопределено Тогда
		АдресСервиса = АдресСервисаПоИмени;
	ИначеЕсли НЕ АдресСервисаПоУмолчанию = Неопределено Тогда
		АдресСервиса = АдресСервисаПоУмолчанию;
	Иначе
		АдресСервиса      = "localhost:1545"; // RAS
	КонецЕсли;
	Если НЕ ВерсияКлиентаПоИмени = Неопределено Тогда
		ВерсияКлиента = ВерсияКлиентаПоИмени;
	ИначеЕсли НЕ ВерсияКлиентаПоУмолчанию = Неопределено Тогда
		ВерсияКлиента = ВерсияКлиентаПоУмолчанию;
	Иначе
		ВерсияКлиента     = "8.3"; // RAC
	КонецЕсли;

	АвторизацияАгента = Новый Структура("Администратор, Пароль", "", "");

	Если ЗначениеЗаполнено(АдминистраторПоИмени) Тогда
		АвторизацияАгента.Администратор = АдминистраторПоИмени;
		Если ЗначениеЗаполнено(ПарольПоИмени) Тогда
			АвторизацияАгента.Пароль = ПарольПоИмени;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(АдминистраторПоУмолчанию) Тогда
		АвторизацияАгента.Администратор = АдминистраторПоУмолчанию;
		Если ЗначениеЗаполнено(ПарольПоУмолчанию) Тогда
			АвторизацияАгента.Пароль = ПарольПоУмолчанию;
		КонецЕсли;
	КонецЕсли;

	Возврат Новый УправлениеКластером1С(ВерсияКлиента, АдресСервиса, АвторизацияАгента);

КонецФункции // ИнициализироватьАгента()

Функция АгентДоступен(ОписаниеАгента)

	Поля = Новый Массив();
	Поля.Добавить("cluster");

	КластерыАгента = ПолныеОписанияКластеровАгента(ОписаниеАгента.Агент);
	
	Попытка
		КоличествоКластеров = КластерыАгента.Количество();
	Исключение
		КоличествоКластеров = 0;
	КонецПопытки;

	Если КоличествоКластеров = 0 Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;

КонецФункции // АгентДоступен()

Функция ИспользоватьАгент(ОписаниеАгента)

	// Если агент не доступен, то он не может использоваться
	Если НЕ АгентДоступен(ОписаниеАгента) Тогда
		Возврат Ложь;
	КонецЕсли;

	// Если резервируемый агент не указан, то это основной агент и используется всегда
	Если НЕ ЗначениеЗаполнено(ОписаниеАгента.Резервирует) Тогда
		Возврат Истина;
	КонецЕсли;

	// Если резервируемый агент не обнаружен, то используем текущий
	Если НЕ (ТипЗнч(Агенты[ОписаниеАгента.Резервирует]) = Тип("Структура")
	   И Агенты[ОписаниеАгента.Резервирует].Свойство("Агент")) Тогда
		Возврат Истина;
	КонецЕсли;

	// Если не удалось получить кластеры резервируемого агента
	// или других резервных агентов того же кластера, то используем текущий
	Для Каждого ТекАгент Из Агенты Цикл
		
		Если ОписаниеАгента.Имя = ТекАгент.Значение.Имя Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекАгент.Значение.Имя = ОписаниеАгента.Резервирует
		 ИЛИ ТекАгент.Значение.Резервирует = ОписаниеАгента.Резервирует Тогда
			Если АгентДоступен(ТекАгент.Значение) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	Возврат Истина;

КонецФункции // ИспользоватьАгент()

// Процедура - устанавливает параметры кластеров агента из настроек
//
// Параметры:
//   УправлениеКластером   - УправлениеКластером1С  - объект управления кластером 1С
//   ПараметрыКластеров    - Соответствие           - настройки кластеров
//
Процедура УстановитьПараметрыКластеровАгента(УправлениеКластером, ПараметрыКластеров)
	
	Если ПараметрыКластеров = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КластерыАгента = Неопределено;

	// TODO: Обход проблемы отсутствия вывода команды rac cluster list
	КоличествоПопыток = 3;
	ПопыткаПолучения = 1;
	КоэффициентОжиданияПопытки = 1500;
	НачалоПопыткиПолучения = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Пока КластерыАгента = Неопределено И ПопыткаПолучения <= КоличествоПопыток Цикл
		КластерыАгента = УправлениеКластером.Кластеры().Список(, Истина);
		Если КластерыАгента = Неопределено Тогда
			Сообщить(СтрШаблон("Неудачная попытка (%1) получить список кластеров у сервиса администрирования ""%2"", ожидаем %3",
			                   ПопыткаПолучения,
			                   УправлениеКластером.СтрокаПодключения(),
			                   КоэффициентОжиданияПопытки * ПопыткаПолучения),
			         СтатусСообщения.ОченьВажное);
			Пока ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоПопыткиПолучения < КоэффициентОжиданияПопытки * ПопыткаПолучения Цикл
			КонецЦикла;
		Иначе
			Прервать;
		КонецЕсли;
		ПопыткаПолучения = ПопыткаПолучения + 1;
	КонецЦикла;

	Если КластерыАгента = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не удалось получить список кластеров у сервиса администрирования ""%1""",
		                            УправлениеКластером.СтрокаПодключения());
	КонецЕсли;

	Для Каждого ТекКластер Из КластерыАгента Цикл
		УстановитьПараметрыКластера(ПараметрыКластеров, ТекКластер);
	КонецЦикла;

КонецПроцедуры // УстановитьПараметрыКластеровАгента()

// Процедура - устанавливает параметры кластера из настроек
//
// Параметры:
//   ПараметрыКластеров - Соответствие   - настройки кластеров
//   Кластер            - Кластер        - объект кластера 1С
//
Процедура УстановитьПараметрыКластера(ПараметрыКластеров, Кластер)
	
	ПараметрыПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("__default", ПараметрыКластеров);
	МеткаКластера        = ВРег(СтрШаблон("%1:%2", Кластер.АдресСервера(), Кластер.ПортСервера()));
	ПараметрыПоИмени     = ОбщегоНазначения.ПолучитьЗначениеНастройки(МеткаКластера, ПараметрыКластеров);
	ПараметрыИБ          = ОбщегоНазначения.ПолучитьЗначениеНастройки("infobase", ПараметрыПоИмени);

	АдминистраторПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоУмолчанию);
	ПарольПоУмолчанию        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоУмолчанию);
	
	АдминистраторПоИмени = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоИмени);
	ПарольПоИмени        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоИмени);

	Если ЗначениеЗаполнено(АдминистраторПоИмени) Тогда
		Администратор = АдминистраторПоИмени;
		Если ЗначениеЗаполнено(ПарольПоИмени) Тогда
			Пароль = ПарольПоИмени;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(АдминистраторПоУмолчанию) Тогда
		Администратор = АдминистраторПоУмолчанию;
		Если ЗначениеЗаполнено(ПарольПоУмолчанию) Тогда
			Пароль = ПарольПоУмолчанию;
		КонецЕсли;
	Иначе
		Администратор = "";
		Пароль        = "";
	КонецЕсли;

	Кластер.УстановитьАдминистратора(Администратор, Пароль);

	Если НЕ ПараметрыИБ = Неопределено Тогда
		СписокИБ = Кластер.ИнформационныеБазы().Список();
		Для Каждого ТекИБ Из СписокИБ Цикл
			УстановитьПараметрыИБ(ПараметрыИБ, ТекИБ);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // УстановитьПараметрыКластера()

// Процедура - устанавливает параметры информационной базы из настроек
//
// Параметры:
//   ПараметрыИБ   - Соответствие        - настройки информационных баз
//   ИБ            - ИнформационнаяБаза  - объект информационной базы 1С
//
Процедура УстановитьПараметрыИБ(ПараметрыИБ, ИБ)
	
	ПараметрыПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("__default", ПараметрыИБ);
	ПараметрыПоИмени = ОбщегоНазначения.ПолучитьЗначениеНастройки(ИБ.Имя(), ПараметрыИБ);

	АдминистраторПоУмолчанию = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоУмолчанию);
	ПарольПоУмолчанию        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоУмолчанию);
	
	АдминистраторПоИмени = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_name", ПараметрыПоИмени);
	ПарольПоИмени        = ОбщегоНазначения.ПолучитьЗначениеНастройки("admin_pwd" , ПараметрыПоИмени);

	Если ЗначениеЗаполнено(АдминистраторПоИмени) Тогда
		Администратор = АдминистраторПоИмени;
		Если ЗначениеЗаполнено(ПарольПоИмени) Тогда
			Пароль = ПарольПоИмени;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(АдминистраторПоУмолчанию) Тогда
		Администратор = АдминистраторПоУмолчанию;
		Если ЗначениеЗаполнено(ПарольПоУмолчанию) Тогда
			Пароль = ПарольПоУмолчанию;
		КонецЕсли;
	Иначе
		Администратор = "";
		Пароль        = "";
	КонецЕсли;

	ИБ.УстановитьАдминистратора(Администратор, Пароль);

КонецПроцедуры // УстановитьПараметрыИБ()

Функция НужноОбновитьОписанияОбъектовКластера(Знач ТипОбъекта, Знач ОбновитьПринудительно = Ложь)

	Если ОбновитьПринудительно Тогда
		Возврат Истина;
	КонецЕсли;

	Результат = Ложь;
	
	Если ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Кластеры) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияКластеров);
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Серверы) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияСерверов);
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().РабочиеПроцессы) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияРабочихПроцессов);
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().ИнформационныеБазы) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияИнформационныхБаз);
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Сеансы) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияСеансов);
	ИначеЕсли ВРег(ТипОбъекта) = ВРег(ТипыОбъектовКластера().Соединения) Тогда
		Результат = НЕ ЗначениеЗаполнено(ОписанияСоединений);
	Иначе
		Результат = Ложь;
	КонецЕсли;

	Если Результат Тогда
		Возврат Результат;
	КонецЕсли;

	Для Каждого ТекАгент Из Агенты Цикл

		Если НЕ ИспользоватьАгент(ТекАгент.Значение) Тогда
			Продолжить;
		КонецЕсли;

		Если ТипОбъекта = ТипыОбъектовКластера().Кластеры Тогда
			Если ТекАгент.Значение.Кластеры().ТребуетсяОбновление(ОбновитьПринудительно) Тогда
				Возврат Истина
			КонецЕсли;
		Иначе
			СписокКластеров = ТекАгент.Значение.Кластеры().Список();
			Для Каждого ТекКластер Из СписокКластеров Цикл
				Если ТекКластер.Получить(ТипОбъекта).ТребуетсяОбновление(ОбновитьПринудительно) Тогда
					Возврат Истина
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции // НужноОбновитьОписанияОбъектовКластера()

Функция ТипыОбъектовКластера()

	Возврат Перечисления.РежимыАдминистрирования;

КонецФункции // ТипыОбъектовКластера()

#КонецОбласти // СлужебныеПроцедурыИФункции
