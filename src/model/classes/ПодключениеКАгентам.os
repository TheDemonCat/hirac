#Использовать irac

Перем Агенты;
Перем ПараметрыПодключения;

#Область ОбработчикиСобытийОбъекта

// Процедура - обработчик события "ПриСозданииОбъекта"
//
// Параметры:
//   НастройкиПодключения     - Строка,     - путь к файлу настроек управления кластерами
//                              Структура     или структура настроек управления кластерами
//
Процедура ПриСозданииОбъекта(Знач НастройкиПодключения = Неопределено)

	ИнициализироватьАгентыУправленияКластерами(НастройкиПодключения);

КонецПроцедуры // ПриСозданииОбъекта()

#КонецОбласти // ОбработчикиСобытийОбъекта

#Область ПрограммныйИнтерфейс

// Функция - возвращает структуру с подключениями к агентам управления кластерами
//
// Возвращаемое значение:
//   Структура     - структура с подключениями к агентам управления кластерами
//
Функция Агенты() Экспорт
	
	Возврат Агенты;

КонецФункции // Агенты()

// Функция - возвращает структуру параметров подключения к агентам управления кластерами
//
// Возвращаемое значение:
//   Структура     - структура параметров подключения к агентам управления кластерами
//
Функция ПараметрыПодключения() Экспорт
	
	Возврат ПараметрыПодключения;

КонецФункции // ПараметрыПодключения()

// Процедура инициализирует подключения к агентам управления кластерами
//
// Параметры:
//   НастройкиПодключения     - Строка,     - путь к файлу настроек управления кластерами
//                              Структура     или структура настроек управления кластерами
//
Процедура ИнициализироватьАгентыУправленияКластерами(Знач НастройкиПодключения = Неопределено) Экспорт
	
	Если ТипЗнч(НастройкиПодключения) = Тип("Структура") Тогда
		ПараметрыПодключения = НастройкиПодключения;
	ИначеЕсли ТипЗнч(НастройкиПодключения) = Тип("Строка") Тогда
		ПараметрыПодключения = ОбщегоНазначения.ПрочитатьДанныеИзМакетаJSON(НастройкиПодключения);
	Иначе
		ПараметрыПодключения = ОбщегоНазначения.ПрочитатьДанныеИзМакетаJSON("/config/racsettings");
	КонецЕсли;

	Если ПараметрыПодключения = Неопределено Тогда
		ПараметрыПодключения = СтруктураПараметровПодключения();
	КонецЕсли;

	Агенты = Новый Структура();

	Для Каждого ТекПараметры Из ПараметрыПодключения Цикл
		АдминистраторАгента = Новый Структура("Администратор, Пароль", "", "");
		Если ЗначениеЗаполнено(ТекПараметры.Значение["admin_name"]) Тогда
			АдминистраторАгента.Администратор = ТекПараметры.Значение["admin_name"];
			Если ЗначениеЗаполнено(ТекПараметры.Значение["admin_pwd"]) Тогда
				АдминистраторАгента.Администратор = ТекПараметры.Значение["admin_pwd"];
			КонецЕсли;
		КонецЕсли;
		УправлениеКластером = Новый УправлениеКластером1С(ТекПараметры.Значение["rac"],
		                                                  ТекПараметры.Значение["ras"],
		                                                  АдминистраторАгента);
		
		ПараметрыКластера = ТекПараметры.Значение["cluster"]["default"];
		
		Кластеры = УправлениеКластером.Кластеры().Список();
		Для Каждого ТекКластер Из Кластеры Цикл
			ТекКластер.УстановитьАдминистратора(ПараметрыКластера["admin_name"], ПараметрыКластера["admin_pwd"]);
		КонецЦикла;

		Агенты.Вставить(ТекПараметры.Ключ, УправлениеКластером);
	КонецЦикла;

КонецПроцедуры // ИнициализироватьАгентыУправленияКластерами()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Функция - возвращает структуру параметров подключения к агентам управления кластерами по умолчанию
//
// Возвращаемое значение:
//   Структура     - структура параметров подключения к агентам управления кластерами по умолчанию
//
Функция СтруктураПараметровПодключения()
	
	ПараметрыКластера = Новый Структура();
	ПараметрыКластера.Вставить("cluster"   , "");
	ПараметрыКластера.Вставить("admin_name", "");
	ПараметрыКластера.Вставить("admin_pwd" , "");

	ПараметрыКластеров = Новый Структура();
	ПараметрыКластеров.Вставить("default", ПараметрыКластера);

	ПараметрыАгента = Новый Структура();
	ПараметрыАгента.Вставить("name"      , "Локальный");
	ПараметрыАгента.Вставить("ras"       , "localhost:1545");
	ПараметрыАгента.Вставить("raс"       , "8.3");
	ПараметрыАгента.Вставить("admin_name", "");
	ПараметрыАгента.Вставить("admin_pwd" , "");
	ПараметрыАгента.Вставить("cluster"   , ПараметрыКластеров);

	ПараметрыАгентов = Новый Структура();
	ПараметрыАгентов.Вставить("default", ПараметрыАгента);

	Возврат ПараметрыАгентов;

КонецФункции // СтруктураПараметровПодключения()

#КонецОбласти // СлужебныеПроцедурыИФункции
