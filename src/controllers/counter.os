
&HTTPMethod("GET")
Функция list() Экспорт

	ТипОбъектов = Неопределено;

	Если ТипЗнч(ЗначенияМаршрута) = Тип("Соответствие") Тогда
		ТипОбъектов = ЗначенияМаршрута.Получить("type");
	КонецЕсли;

	Результат = ПолучениеСчетчиков.Список(ТипОбъектов);

	Возврат Содержимое(Результат);

КонецФункции // list()

&HTTPMethod("GET")
Функция get() Экспорт

	ТипОбъектов = Неопределено;
	Счетчик = Неопределено;

	Если ТипЗнч(ЗначенияМаршрута) = Тип("Соответствие") Тогда
		ТипОбъектов = ЗначенияМаршрута.Получить("type");
		Счетчик = ЗначенияМаршрута.Получить("counter");
	КонецЕсли;
	
	ПараметрыЗапроса = ЗапросHTTP.ПараметрыЗапроса();

	Формат = "json";
	Если НЕ ПараметрыЗапроса["format"] = Неопределено Тогда
		Формат = ПараметрыЗапроса["format"];
	КонецЕсли;

	Фильтр = ОбщегоНазначения.ФильтрИзПараметровЗапроса(ПараметрыЗапроса);

	Измерения = "";
	Если НЕ ПараметрыЗапроса["dim"] = Неопределено Тогда
		Измерения = ПараметрыЗапроса["dim"];
	КонецЕсли;

	АгрегатнаяФункция = "count";
	Если НЕ ПараметрыЗапроса["agregate"] = Неопределено Тогда
		АгрегатнаяФункция = ПараметрыЗапроса["agregate"];
	КонецЕсли;

	ПараметрыСчетчиков = ПолучениеСчетчиков.ПараметрыСчетчиков();
	ПараметрыСчетчика = ПараметрыСчетчиков[ТипОбъектов];

	Поля = "";

	Для Каждого ТекИзмерение Из ПараметрыСчетчика["dimentions"] Цикл
		Если ТекИзмерение.Значение.Свойство("name_rac") Тогда
			ИмяПоля = ТекИзмерение.Значение["name_rac"];
			Если ВРег(Прав(ИмяПоля, 6)) = "_LABEL" Тогда
				ИмяПоля = Сред(ИмяПоля, 1, СтрДлина(ИмяПоля) - 6);
			КонецЕсли;
		Иначе
			ИмяПоля = ТекИзмерение.Ключ;
		КонецЕсли;
		Поля = Поля + ?(ЗначениеЗаполнено(Поля), ", ", "") + ИмяПоля;
	КонецЦикла;

	Для Каждого ТекСчетчик Из ПараметрыСчетчика["counters"] Цикл
		Если ТекСчетчик.Значение.Свойство("name_rac") Тогда
			ИмяПоля = ТекСчетчик.Значение["name_rac"];
		Иначе
			ИмяПоля = ТекСчетчик.Ключ;
		КонецЕсли;
		Поля = Поля + ?(ЗначениеЗаполнено(Поля), ", ", "") + ИмяПоля;
	КонецЦикла;

	Поля = ?(ЗначениеЗаполнено(Поля), Поля, "_all");

	Если ТипОбъектов = "server" Тогда
		Данные = ДанныеСерверов.Серверы(Поля, Фильтр, Истина);
	ИначеЕсли ТипОбъектов = "process" Тогда
		Данные = ДанныеПроцессов.Процессы(Поля, Фильтр, Истина);
	ИначеЕсли ТипОбъектов = "infobase" Тогда
		Данные = ДанныеИБ.ИнформационныеБазы(Поля, Фильтр, Истина);
	ИначеЕсли ТипОбъектов = "session" Тогда
		Данные = ДанныеСеансов.Сеансы(Поля, Фильтр, Истина);
	ИначеЕсли ТипОбъектов = "connection" Тогда
		Данные = ДанныеСоединений.Соединения(Поля, Фильтр, Истина);
	Иначе
		Возврат Содержимое(СтрШаблон("Не обнаружены данные для типа объектов ""%1""!", ТипОбъектов));
	КонецЕсли;
	
	Результат = ПолучениеСчетчиков.Счетчики(Данные,
	                                        ТипОбъектов,
	                                        Счетчик,
	                                        Фильтр,
	                                        Измерения,
	                                        АгрегатнаяФункция,
	                                        Формат);

	Возврат Содержимое(Результат);

КонецФункции // get()
